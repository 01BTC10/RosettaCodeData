void
fs(integer &s, data b)
{
    if (s) {
	b_append(b, ',');
    }
    s = 1;
}

text
rp(...)
{
    integer f, i, j;
    data b;

    f = 0;
    i = 0;
    j = 0;
    while (i < count()) {
	while (j < count() - 1) {
	    if (__integer(lead(j + 1)) == __integer(lead(j)) + 1) {
		j += 1;
	    } else {
		break;
	    }
	}
	if (i + 1 < j) {
	    fs(f, b);
	    b_suffix(b, itoa(lead(i)));
	    b_append(b, '-');
	    b_suffix(b, itoa(lead(j)));
	} else {
	    while (i < j + 1) {
		fs(f, b);
		b_suffix(b, itoa(lead(i)));
		i += 1;
	    }
	}
	j += 1;
	i = j;
    }

    return b_string(b);
}

integer
main(void)
{
    o_text(rp(0, 1, 2, 4, 6, 7, 8, 11, 12, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24,
	      25, 27, 28, 29, 30, 31, 32, 33, 35, 36, 37, 38, 39));
    o_byte('\n');

    return 0;
}
