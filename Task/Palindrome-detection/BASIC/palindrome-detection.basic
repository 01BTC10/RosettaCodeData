DECLARE FUNCTION isPalindrome% (what AS STRING)

DATA "My dog has fleas", "Madam, I'm Adam.", "1 on 1", "In girum imus nocte et consumimur igni"

DIM L1 AS INTEGER, w AS STRING
FOR L1 = 1 TO 4
    READ w
    IF isPalindrome(w) THEN
        PRINT CHR$(34); w; CHR$(34); " is a palindrome"
    ELSE
        PRINT CHR$(34); w; CHR$(34); " is not a palindrome"
    END IF
NEXT

FUNCTION isPalindrome% (what AS STRING)
    DIM whatcopy AS STRING, chk AS STRING, tmp AS STRING * 1, L0 AS INTEGER

    FOR L0 = 1 TO LEN(what)
        tmp = UCASE$(MID$(what, L0, 1))
        SELECT CASE tmp
            CASE "A" TO "Z"
                whatcopy = whatcopy + tmp
                chk = tmp + chk
            CASE "0" TO "9"
                PRINT "Numbers are cheating! ("; CHR$(34); what; CHR$(34); ")"
                isPalindrome = 0
                EXIT FUNCTION
        END SELECT
    NEXT

    isPalindrome = ((whatcopy) = chk)
END FUNCTION
