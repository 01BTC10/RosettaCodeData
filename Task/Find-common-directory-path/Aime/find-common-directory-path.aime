text
cdp(...)
{
    record r;
    integer i;
    text s;

    i = -count();
    while (i) {
        r_add(r, $i, 0);
        i += 1;
    }

    if (r_size(r)) {
        integer c;
        text v;

        r_first(r, s);
        r_last(r, v);

        i = 0;
        while ((c = character(s, i)) == character(v, i) && c) {
            i += 1;
        }

        if (!c && character(v, i) == '/') {
        } else {
            while (i && character(s, i - 1) != '/') {
                i -= 1;
            }
        }

        if (1 < i && character(s, i - 1) == '/') {
            i -= 1;
        }

        s = cut(s, 0, i);
    }

    return s;
}

integer
main(void)
{
    o_text(cdp("/home/user1/tmp/coverage/test",
               "/home/user1/tmp/covert/operator",
               "/home/user1/tmp/coven/members"));
    o_byte('\n');

    return 0;
}
