integer
F(list l)
{
    return l_q_integer(l, 1);
}

integer (*type(integer (*f) (list))) (list)
{
    return f;
}

integer
eval(list l)
{
    return type(l_query(l, 0))(l);
}

integer A(list);

integer
B(list l)
{
    integer x;
    list a;

    x = l_q_integer(l, 1);
    x -= 1;
    l_r_integer(l, 1, x);

    l_append(a, B);
    l_append(a, x);
    l_l_list(a, -1, l);
    l_l_list(a, -1, l_query(l, -5));
    l_l_list(a, -1, l_query(l, -4));
    l_l_list(a, -1, l_query(l, -3));
    l_l_list(a, -1, l_query(l, -2));

    return A(a);
}

integer
A(list l)
{
    integer x;

    if (l_q_integer(l, 1) < 1) {
	x = eval(l_q_list(l, -2)) + eval(l_q_list(l, -1));
    } else {
	x = B(l);
    }

    return x;
}

integer
main(void)
{
    list a, f1, f0, fn1;

    l_append(f1, F);
    l_append(f1, 1);

    l_append(f0, F);
    l_append(f0, 0);

    l_append(fn1, F);
    l_append(fn1, -1);

    l_append(a, B);
    l_append(a, 10);
    l_l_list(a, -1, f1);
    l_l_list(a, -1, fn1);
    l_l_list(a, -1, fn1);
    l_l_list(a, -1, f1);
    l_l_list(a, -1, f0);

    o_integer(A(a));
    o_byte('\n');

    return 0;
}
